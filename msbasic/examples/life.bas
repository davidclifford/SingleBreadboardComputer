10 BB=0 : REM CONFIG, 1=BEEB, 0=COMMODORE
20 IF BB=0 THEN PRINTCHR$(147);CHR$(18);:GOTO 30
25 MODE 7
30 PRINT"  CONWAY'S GAME OF LIFE  "
40 PRINT:PRINT"IN MEMORIAM JOHN H CONWAY"
50 PRINT"26 DEC 1937 - 11 APR 2020":PRINT
60 INPUT"WIDTH, HEIGHT (EG, 8,8)";W,H
70 IF W<4 THEN W=4
80 IF W>39 THEN W=39
90 IF H<4 THEN H=4
100 IF H>24 THEN H=24
110 G=0:A=0:P=0:W=W+1:H=H+1:D1=W+1:D2=W-1:K=0:DG=W*H+W:NG=DG
120 XM=W-1:YM=W*H-W:N=0:IF BB=0 THEN 140
130 CS=12:CH=30:CO=79-32:R=1:GOTO150
140 CS=42:CH=46:CO=35-32:R=1
150 PRINT:PRINT"PROBABILITY OF INITIAL POPULATION:":INPUT"(0>P<1)";P
160 IF P<=0 OR P>=1 THEN P=0.5
170 DIM M%(DG+DG):PRINTCHR$(CS);
180 FORY=W TOYM STEPW:FORX=1TOXM:M%(G+Y+X)=RND(R)<P:NEXT:NEXT
185 PRINTCHR$(27);"[2J";
190 PRINTCHR$(27);"[H";CHR$(CH);"GEN.:";N
200 FORY=W TOYM STEPW:FORX=1TOXM:PRINTCHR$(32-M%(G+Y+X)*CO);
205 NEXT:PRINT:NEXT
210 FORY=W TOYM STEPW:FORX=1TOXM:A=G+Y+X
220 K=M%(A-D1)+M%(A-W)+M%(A-D2)+M%(A-1)
225 K=K+M%(A+1)+M%(A+D2)+M%(A+W)+M%(A+D1)
230 M%(NG+Y+X)=(K=-3)OR(K=-2 ANDM%(A)=-1)
240 NEXT:NEXT:G=NG:NG=DG-G:N=N+1:GOTO190
